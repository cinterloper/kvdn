
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.co.riiid:gradle-github-plugin:0.4.2"
    }
}


plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'maven-publish'

}


apply plugin: "co.riiid.gradle"
apply plugin: 'distribution'



def libversion = '3.3.2'
if(System.getenv("VERTX_VERSION") != null)
    libversion = System.getenv("VERTX_VERSION")

version =  "$libversion-1.0.6"
def GITHUB_TOKEN = System.getenv("GITHUB_TOKEN")
def AWS_MVN_BUCKET = System.getenv("AWS_MVN_BUCKET")
def AWS_DEFAULT_REGION = System.getenv("AWS_DEFAULT_REGION")
def AWS_ACCESS_KEY_ID = System.getenv("AWS_ACCESS_KEY_ID")
def AWS_SECRET_ACCESS_KEY = System.getenv("AWS_SECRET_ACCESS_KEY")
def USER = System.getenv("USER")


distributions {
    clients {
        baseName = 'clients'
        contents {
            from { 'Client/' }
        }
    }
}

if(System.getenv("GITHUB_TOKEN") != null) {
    github {
        owner = 'cinterloper'
        repo = 'kvdn'
        token = GITHUB_TOKEN
        tagName = version
        targetCommitish = 'master'
        name = "v$version"
        body = """# KVDN
release $version
${System.getenv('RELEASE_NOTES')?:""}
"""

        assets = [

                "build/libs/kvdn-$version-fat.jar",
                "build/distributions/clients-${version}.zip",
                "build/distributions/clients-${version}.tar"
        ]
    }
}
repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'http://dl.bintray.com/gesellix/docker-utils' }
    flatDir { dirs '/usr/share/java' }
    flatDir { dirs 'lib/' }

    if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
        maven {
            url "s3://$AWS_MVN_BUCKET/maven2"
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY_ID
                secretKey AWS_SECRET_ACCESS_KEY
            }
        }
    }
}
publishing {
    repositories {
	mavenLocal()
        if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
            maven {
                url "s3://$AWS_MVN_BUCKET/maven2"
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY_ID
                    secretKey AWS_SECRET_ACCESS_KEY
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId 'net.iowntheinter'
            artifactId 'kvdn'
            from components.java
        }
    }
}

sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

dependencies {
    compile "io.vertx:vertx-web:$libversion"
    compile "io.vertx:vertx-core:$libversion"
    compile "com.google.guava:guava:19.0"
    compile "io.vertx:vertx-lang-groovy:$libversion"
    compile "io.vertx:vertx-auth-common:$libversion"
    compile "io.vertx:vertx-auth-jwt:$libversion"

}

processResources {
    from 'src/main/groovy'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'groovy:kvdn-start.groovy'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
